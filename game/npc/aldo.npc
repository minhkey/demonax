# GIMUD - Graphical Interface Multi User Dungeon
# aldo.npc: Datenbank f?r den Schuhverk?ufer Aldo

Name = "Aldo"
Sex = male
Race = 1
Outfit = (128,40-37-116-76)
Home = [32953,32110,6]
Radius = 2
GoStrength = 10

Behaviour = {
ADDRESS,"hello$",! -> "Just great, another ... 'customer'. Hello, %N."
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Yes, yes, I can only talk to one after the other! You will have to wait, %N.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Yeah, get lost."

"bye"       -> "That's music in my ears.", Idle
"name"      -> "I'm Aldo. No one calls me 'lucky Aldo' though, guess why!"
"job"       -> "I am a salesman, I sell headgear ... uhm ... oh well, and shoes."
"time"      -> "Is it time for lunch already? Hey, stop making fun of me!"
"king"      -> "One day I will sell the king a pair of shoes made by me and will get out of that stinky hole I live in and my family will never find me. HE, HE!"
"tibianus"  -> *
"army"      -> "So many feet ... so many ... a nightmare!"
"ferumbras" -> "Can't be worse than my wife. I hear he's back?"
"wife"      -> "Leave me alone with her while I am working at least. My only pleasure around here!"
"excalibug" -> "I have other stuff to worry about, like paying my bills."
"bill"      -> "Yes, I have to pay o lot of bills, and some georges, and a john, and several steves."
"thais"     -> "I will never in my life make it there."
"demonax"   -> "I doubt I will ever see much of it. It's like i am cursed to haunt this site here for the rest of my life."
"carlin"    -> "A city ruled by women!? Could anything be worse?"
"amazon"    -> "I heard that chicks wear some revealing pieces of armor!"
"news"      -> "Hey, I am a man. Look for some women to share gossip."
"rumour"    -> *
"rumor"     -> *
"hugo"      -> "My boss, an evil slaver of good people like me."

"offer"           -> "I am damned to sell headgear, trousers, and shoes for the rest of my life."
"goods"           -> *
"do","you","sell" -> *
"do","you","have" -> *
"headgear"        -> "We have leather helmets and studded helmets."
"shoes"           -> "<sigh> We sell leather boots and sandals."
"trouser"         -> "We offer leather legs and studded legs."

"leather","helmet" -> Type=3355, Amount=1, Price=12, "Do you want to buy a leather helmet for %P gold?", Topic=1
"studded","helmet" -> Type=3376, Amount=1, Price=63, "Do you want to buy a studded helmet for %P gold?", Topic=1
"leather","boot"   -> Type=3552, Amount=1, Price=2,  "Do you want to buy one of my wonderful leather boots for %P gold?", Topic=1
"leather","legs"   -> Type=3559, Amount=1, Price=10, "Do you want to buy leather legs for %P gold?", Topic=1
"studded","legs"   -> Type=3362, Amount=1, Price=60, "Do you want to buy studded legs for %P gold?", Topic=1

%1,1<%1,"leather","helmet" -> Type=3355, Amount=%1, Price=12*%1, "Do you want to buy %A leather helmets for %P gold?", Topic=1
%1,1<%1,"studded","helmet" -> Type=3376, Amount=%1, Price=63*%1, "Do you want to buy %A studded helmets for %P gold?", Topic=1
%1,1<%1,"leather","boot"   -> Type=3552, Amount=%1, Price=2*%1,  "Do you want to buy %A of my wonderful leather boots for %P gold?", Topic=1
%1,1<%1,"leather","legs"   -> Type=3559, Amount=%1, Price=10*%1, "Do you want to buy %A leather legs for %P gold?", Topic=1
%1,1<%1,"studded","legs"   -> Type=3362, Amount=%1, Price=60*%1, "Do you want to buy %A studded legs for %P gold?", Topic=1

Topic=1,"yes",CountMoney>=Price -> "Here. I hope that's it now.", DeleteMoney, Create(Type)
Topic=1,"yes"                   -> "At last, someone poorer than me."
Topic=1,"no"                    -> "Good decision!"

"repair",QuestValue(203)<1      -> "It is true that I can weave magic back into worn soft boots. But only for those actually experienced enough to use them."
"repair",QuestValue(203)>0      -> Type=5091, Amount=1, Price=15000, "So, you want me to turn these into a pair of soft boots? Well, you seem worthy enough. %P gold, deal?", Topic=2

Topic=2,"yes",Count(Type)>=Amount,CountMoney>=Price -> "There you go.", DeleteMoney, Delete(Type), Create(3246)
Topic=2,"yes"                                       -> "At last, someone poorer than me."
Topic=2,"no"                                        -> "Good decision!"

"sell",%1,1<%1,"soft","boots"  -> Type=3549, Amount=%1, Price=185000*%1,   "Oh, you want to get rid of them? All right, %P gold, ok?", Topic=3
"sell","soft","boots"          -> Type=3549, Amount=1, Price=185000,       "Oh, you want to get rid of them? All right, %P gold, ok?", Topic=3

Topic=3,"yes",Count(Type)>=Amount -> "Ok. Here is your money.", Delete(Type), CreateMoney
Topic=3,"yes"                             -> "Hmpf, either you do not have any, or you are still wearing them. And I will not undress you."
Topic=3,"yes",Amount>1                    -> "Hmpf, you do not have that many."
Topic=3                                   -> "Maybe next time. Or not."
}
