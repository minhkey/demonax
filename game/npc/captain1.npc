# GIMUD - Graphical Interface Multi User Dungeon
# captain1.npc: Kapit?n Blaubaer in Thais

Name = "Captain Bluebear"
Sex = male
Race = 1
Outfit = (134,19-69-107-50)
Home = [32310,32210,6]
Radius = 2
GoStrength = 5

Behaviour = {
ADDRESS,"hello$",male,!   -> "Welcome on board, Sir %N."
ADDRESS,"hi$",male,!      -> *
ADDRESS,"hello$",female,! -> "Welcome on board, Madam %N."
ADDRESS,"hi$",female,!    -> *
ADDRESS,!                 -> Idle
BUSY,"hello$",!           -> "One moment please %N. You're next in line.", Queue
BUSY,"hi$",!              -> *
BUSY,!                    -> NOP
VANISH,!                  -> "Good bye. Recommend us, if you were satisfied with our service."

"bye"          -> "Good bye. Recommend us, if you were satisfied with our service.", Idle
"farewell"     -> *
"name"         -> "My name is Captain Bluebear from the Royal Demonax Line."
"job"          -> "I am the captain of this sailing-ship."
"captain"      -> *
"ship"         -> "The Royal Demonax Line connects all seaside towns of Demonax."
"line"         -> *
"company"      -> *
"route"        -> *
"demonax"        -> *
"good"         -> "We can transport everything you want."
"passenger"    -> "We would like to welcome you on board."
"trip"         -> "Where do you want to go? To Carlin, Ab'Dendriel, Venore, Port Hope or Edron?"
"passage"      -> *
"town"         -> *
"destination"  -> *
"sail"         -> *
"go"           -> *
"ice"          -> "I'm sorry, but we don't serve the routes to the Ice Islands."
"senja"        -> *
"folda"        -> *
"vega"         -> *
"fogn"        -> *
"darashia"     -> "I'm not sailing there. This route is afflicted by a ghostship! However I've heard that Captain Fearless from Venore sails there."
"darama"       -> *
"ghost"        -> "Many people who sailed to Darashia never returned because they were attacked by a ghostship! I'll never sail there!"

"thais"       -> "This is Thais. Where do you want to go?"
"carlin"      -> Price=110, "Do you seek a passage to Carlin for %P gold?", Topic=2
"ab'dendriel" -> Price=130, "Do you seek a passage to Ab'Dendriel for %P gold?", Topic=3
"edron"       -> Price=160,  "Do you seek a passage to Edron for %P gold?", Topic=4
"venore"      -> Price=170, "Do you seek a passage to Venore for %P gold?", Topic=5
"port","hope" -> Price=160, "Do you seek a passage to Port Hope for %P gold?", Topic=7
"undead","island" -> Price=290, "Oh, you're a brave one. Do you seek a passage to the Undead Island for %P gold?", Topic=8

"carlin",QuestValue(250)>2      -> Price=100, "Do you seek a passage to Carlin for %P gold?", Topic=2
"ab'dendriel",QuestValue(250)>2 -> Price=120, "Do you seek a passage to Ab'Dendriel for %P gold?", Topic=3
"edron",QuestValue(250)>2       -> Price=150,  "Do you seek a passage to Edron for %P gold?", Topic=4
"venore",QuestValue(250)>2      -> Price=160, "Do you seek a passage to Venore for %P gold?", Topic=5
"port","hope",QuestValue(250)>2 -> Price=150, "Do you seek a passage to Port Hope for %P gold?", Topic=7
"undead","island",QuestValue(250)>2 -> Price=200, "Oh, you're a brave one. Do you seek a passage to the Undead Island for %P gold?", Topic=8

"argonia",QuestValue(462)=0    -> "Argonia! Yes. It is a rather long journey. Tell you what, do me a favour first, then I will take you there. ...",
                                  "Go see Roderick, the King's ambassador, in Ab'Dendriel. I have ordered a model of my beloved ship, and he would arrange it. ...",
                                  "Apparently, there are capable craftsmen in Ab'Dendriel. Bring the ship to me, and I will get you to Argonia!", SetQuestValue(462,1), Idle

"argonia",QuestValue(462)<8    -> "Ah, %N! No sign of my model ship yet, I see.", Idle
"argonia",QuestValue(462)=8    -> "Finally, %N! Did you bring my model ship?", Topic=9
"argonia",QuestValue(462)>8    -> Price=345, "A long journey. Do you want to sail to Argonia for %P gold?", Topic=10

Topic=9,"yes",Count(2994)=1,!        -> "Oh, how beatiful it is! I will take you to Argonia now. And since it took longer than expected, go see Roderick again. He has a surprise for you!", Amount=1, Delete(2994), SetQuestValue(462,9), Idle
Topic=9                              -> "Well, where is it then?", Idle


Topic>0,Topic<9,"yes",PZBlock,! -> "First get rid of those blood stains! You are not going to ruin my vehicle!"

#Topic=2,"yes",PZBlock,! -> "First get rid of those blood stains! You are not going to ruin my vehicle!"
#Topic=3,"yes",PZBlock,! -> *
#Topic=4,"yes",PZBlock,! -> *
#Topic=5,"yes",PZBlock,! -> *

# f?r postquest
Topic=2,"yes",Premium, QuestValue(227)=1,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32387,31821,6), EffectOpp(11),SetQuestValue(227,2)

Topic=2,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32387,31821,6), EffectOpp(11)
Topic=3,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32733,31668,6), EffectOpp(11)
Topic=4,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(33175,31764,6), EffectOpp(11)
Topic=5,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32954,32023,6), EffectOpp(11)
Topic=7,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32530,32784,6), EffectOpp(11)
Topic=8,"yes",Premium,CountMoney>=Price -> "May the Gods protect you!", DeleteMoney, Idle, EffectOpp(11), Teleport(32118,32722,7), EffectOpp(11)
Topic=10,"yes",Premium,CountMoney>=Price -> "Set the sails!", DeleteMoney, Idle, EffectOpp(11), Teleport(32294,32537,6), EffectOpp(11)

Topic>0,"yes",CountMoney>=Price         -> "I'm sorry, but you need a premium account in order to travel onboard our ships."
Topic>0,"yes"                           -> "You don't have enough money."
Topic>0                                 -> "We would like to serve you some time."
}
